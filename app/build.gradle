apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: "androidx.navigation.safeargs.kotlin"

apply from: '../dependencies.gradle'
apply from: '../configuration.gradle'

apply from: "../scripts/icons.gradle"
apply from: "../scripts/signing.gradle"

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion configuration.compileSdkVersion

    defaultConfig {
        applicationId configuration.applicationId
        minSdkVersion configuration.minSdkVersion
        targetSdkVersion configuration.targetSdkVersion
        versionCode configuration.versionCode
        versionName configuration.versionName
        testInstrumentationRunner configuration.androidJUnitRunner
        multiDexEnabled false

        buildConfigField "String", "FLURRY_API_KEY", "\"${configuration.flurryApiKey}\""
    }

    signingConfigs {
        release {}
    }

    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            multiDexEnabled true
        }
        beta {
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
            minifyEnabled true
            debuggable true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            debuggable false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation kotlin
    implementation coroutinesDependencies.values()

    implementation androidFrameworkDependencies.values()
    implementation androidViewDependencies.values()

    implementation archComponentsDependencies.values()
    kapt archComponentsProcessor

    implementation firebaseDependencies.values()

    androidTestImplementation androidTestDependencies.values()
    testImplementation testDependencies.values()

    kaptAndroidTest daggerProcessor

    implementation ktxDependencies.values()
    implementation glide
    implementation lottie

    implementation dagger
    kapt daggerProcessor

    implementation roomDependencies.values()
    kapt roomProcessor

    implementation networkDependencies.values()

    implementation flurryDependencies.values()

    debugImplementation leekcanary

    api project(path: ':data')
    implementation project(path: ':domain')
}